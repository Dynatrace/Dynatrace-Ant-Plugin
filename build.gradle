buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
		
		maven {
			url "https://plugins.gradle.org/m2/"
		}
    }
	
    dependencies {
        /* required for jar shadowing */
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3+'
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

/* IDE projects generation */
apply plugin: 'eclipse'
apply plugin: 'idea'

/* java configuration */
apply plugin: 'java'


compileJava {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

/* shadowJar */
apply plugin: 'com.github.johnrengelman.shadow'

configurations {
    compileAndIncludeInJar
    compile.extendsFrom(configurations.compileAndIncludeInJar)
}
	
shadowJar {
    configurations = [ project.configurations.compileAndIncludeInJar ]
	classifier = null
}

build.finalizedBy shadowJar

task dist(type: Copy) {
    from './dtTaskDefs.xml'
    from './build/libs'
	
    into './build/dist'
}

shadowJar.finalizedBy dist

/* compile and testing dependencies */
dependencies {
	compileAndIncludeInJar fileTree(dir: 'lib', include: '*.jar')
	compile 'org.apache.ant:ant:1.7.1'
	
	testCompile 'junit:junit:4.12'
	testCompile 'org.apache.ant:ant:1.7.1'
	testCompile 'org.apache.ant:ant-testutil:1.9.7'
	testCompile 'org.mockito:mockito-core:1.10.19'
	testCompile 'org.powermock:powermock-mockito-release-full:1.6.2'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.0'
}